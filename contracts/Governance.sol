// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

import {LibDiamond} from './utils/LibDiamond.sol';
import {IDiamondCut} from './interfaces/IDiamondCut.sol';

contract Governance {
    constructor(address worker, address diamondCutFacet) {
        LibDiamond.setWorker(worker);

        IDiamondCut.FacetCut[] memory cut = new IDiamondCut.FacetCut[](1);
        bytes4[] memory functionSelectors = new bytes4[](1);
        functionSelectors[0] = IDiamondCut.diamondCut.selector;
        cut[0] = IDiamondCut.FacetCut({
            facetAddress: diamondCutFacet,
            action: IDiamondCut.FacetCutAction.Add,
            functionSelectors: functionSelectors
        });
        LibDiamond.diamondCut(cut, address(0), '');
    }

    fallback() external payable {
        address facet = LibDiamond.localStorage().selectorToFacetAndPosition[msg.sig].facetAddress;
        // Execute external function from facet using delegatecall and return any value.
        assembly {
            // copy function selector and any arguments
            calldatacopy(0, 0, calldatasize())
            // execute function call using the facet
            let result := delegatecall(gas(), facet, 0, calldatasize(), 0, 0)
            // get any return value
            returndatacopy(0, 0, returndatasize())
            // return any return value or error back to the caller
            switch result
                case 0 {
                    revert(0, returndatasize())
                }
                default {
                    return(0, returndatasize())
                }
        }
    }
}